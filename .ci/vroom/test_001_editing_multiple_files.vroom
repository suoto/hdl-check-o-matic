Macro that checks if the quickfix is empty
  @macro (assert_qf_empty)
  :echom &ft
  ~ vhdl
  :silent! SyntasticCheck
  :silent! lopen
  :echom getline('.')
  &
  :quit
  @endmacro

Macro that checks if vimhdl is enabled
  @macro (vimhdl_check)
  :SyntasticInfo
  ~ Syntastic version:.* (regex)
  ~ Info for filetype: vhdl
  ~ Global mode: active
  ~ Filetype vhdl is active
  ~ The current file will be checked automatically
  ~ Available checker(s:|:)\s*.*\bvimhdl\b.* (regex)
  ~ Currently enabled checker(s:|:)\s*.*\bvimhdl\b.* (regex)
  @endmacro

Macro that writes and waits for the save msg (seems needed in some cases)
  @macro (write)
  :write
  ~ ".*".*\swritten (regex)
  @endmacro




Edit different files under the same project
  :let g:vimhdl_conf_file = getcwd() . '/../hdlcc_ci/hdl_lib/ghdl.prj'
  :cd ../hdlcc_ci/hdl_lib
  :edit common_lib/edge_detector.vhd
  @do (vimhdl_check)

Again, this file has no errors
  @do (assert_qf_empty)

Remove an important line so the builder must tell there's an error
  > /^end\s\+edge_detector<CR>
  > dd
  :echom getline('.')
  ~ end edge_detector;
  @do (write)
  :silent! SyntasticCheck
  :silent! lopen
  common_lib/edge_detector.vhd|36 col 1 error| 'end' is expected instead of 'architecture'
  :lclose
  > u
  @do (write)
  @do (assert_qf_empty)



Ok, so the previous file worked fine. Let's open another file that our edge_detector
depends on, like the sr_delay
  :edit common_lib/sr_delay.vhd
Again, this file has no errors
  @do (write)
  :lclose
  @do (assert_qf_empty)

Let's rename the 'clken' port to 'clk_en'
  :%s/\<clken\>/clk_en/g
This won't cause errors for this file
  @do (write)
  :lclose
  @do (assert_qf_empty)



But our edge detector should warn that a port named 'clken' doens't exist
anymore
  :edit common_lib/edge_detector.vhd
  @do (write)
  :silent! lopen
  common_lib/edge_detector.vhd|75 col 24 error| no interface for 'clken' in association
  :lclose

Let's undo our changes
  :edit common_lib/sr_delay.vhd
  :%s/\<clk_en\>/clken/g
  @do (write)
  :lclose
  @do (assert_qf_empty)

Again, this should propagate
  :edit common_lib/edge_detector.vhd
  @do (write)
  :lclose
  @do (assert_qf_empty)



We'll edit a packge that both sr_delay and the edge_detector depend. Let's see
what happens!
  :edit common_lib/common_pkg.vhd
  :lclose
  @do (assert_qf_empty)
  :10
  > o constant SOME_CONSTANT : integer := 14<ESC>
  @do (write)
  :silent! SyntasticCheck
  :silent! lopen
  common_lib/common_pkg.vhd|12 col 5 error| ';' is expected instead of 'function'
  :q

  > a;<ESC>
  @do (write)
  :lclose
  @do (assert_qf_empty)


The package has changed but neither the edge_detector or sr_delay have been
rebuilt
  :edit common_lib/edge_detector.vhd
  @do (write)
  :lclose
  @do (assert_qf_empty)

Let's rebuild
  :edit common_lib/synchronizer.vhd
  @do (write)
  :lclose
  @do (assert_qf_empty)

Let's rebuild
  :edit common_lib/sr_delay.vhd
  @do (write)
  :lclose
  @do (assert_qf_empty)


The package has changed but neither the edge_detector or sr_delay have been
rebuilt
  :edit common_lib/edge_detector.vhd
  @do (write)
  :lclose
  @do (assert_qf_empty)



Corrupt hdlcc database, check it actually fails and then ask it to rebuild to
confirm it works again
  :edit common_lib/edge_detector.vhd
  @do (write)
  :lclose
  @do (assert_qf_empty)
Delete some internals
  @system (RELAXED)
  :!rm .build/common_lib*
  ! rm .build/common_lib.* (regex)
  @system
Checking right now should fail
  :silent! SyntasticCheck
  :silent! lopen
  common_lib/edge_detector\.vhd|13 col 20 error| (primary)? unit "common_pkg" not found in library "common_lib" (regex)
  :lclose
Rebuild and check that there's no errors
  :VimhdlRebuildProject
  :silent! SyntasticCheck
  @do (assert_qf_empty)
