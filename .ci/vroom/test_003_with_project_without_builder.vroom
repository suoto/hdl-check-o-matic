Macro that checks if the quickfix is empty
  @macro (assert_qf_empty)
  :echom &ft
  ~ vhdl
  :silent! SyntasticCheck
  :silent! lopen
  :echom getline('.')
  &
  :quit
  @endmacro

We'll test what happens when we configure a project but the builder it specifies
is not found, which should trigger using the fallback builder
  :let hdl_lib_path = getcwd() . '/../hdlcc_ci/hdl_lib'
  :let g:vimhdl_conf_file = hdl_lib_path . '/msim.prj'
  :cd ../hdlcc_ci/hdl_lib
  :edit common_lib/edge_detector.vhd
  ~ "common_lib/edge_detector.vhd".* (regex)
  > i<ESC>
  ~ Failed to create builder 'msim'

  @do (assert_qf_empty)

Change the project filename to a builder we have configured
  :let g:vimhdl_conf_file = hdl_lib_path . '/ghdl.prj'
  :write (2.0)
  @do (assert_qf_empty)

Remove an important line so the builder must tell there's an error
  > /^end\s\+edge_detector<CR>
  > dd
  :echom getline('.')
  ~ end edge_detector;
  :write
  ~ ".*".*\swritten (regex)
  :silent! SyntasticCheck
  :silent! lopen
  common_lib/edge_detector.vhd|36 col 1 error| 'end' is expected instead of 'architecture'
  :lclose
  > u
  :write
  @do (assert_qf_empty)

